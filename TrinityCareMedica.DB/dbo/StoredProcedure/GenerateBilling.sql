CREATE PROCEDURE [dbo].[GenerateBilling]
    @AdmissionID INT
AS
BEGIN
    SET NOCOUNT ON;

    DECLARE @PatientID INT,
            @RoomCharges DECIMAL(10,2),
            @TreatmentCharges DECIMAL(10,2),
            @MedicationCharges DECIMAL(10,2),
            @TotalAmount DECIMAL(10,2);

    SELECT @PatientID = PatientID
    FROM AdmissionHistory
    WHERE AdmissionID = @AdmissionID;

    SELECT @RoomCharges = ISNULL(SUM((DATEDIFF(DAY, RA.StartDate, ISNULL(RA.EndDate, GETDATE())) + 1) * R.RatePerDay), 0)
    FROM RoomAssignments RA
    INNER JOIN Rooms R ON RA.RoomID = R.RoomID
    WHERE RA.AdmissionID = @AdmissionID;

    SELECT @TreatmentCharges = ISNULL(SUM(T.Price), 0)
    FROM Treatments T
    INNER JOIN MedicalRecords MR ON T.RecordID = MR.RecordID
    WHERE MR.AdmissionID = @AdmissionID;

    SELECT @MedicationCharges = ISNULL(SUM(M.FrequencyCount * (24 / M.FrequencyInterval) * M.Duration * M.Price), 0)
    FROM Medications M
    INNER JOIN MedicalRecords MR ON M.RecordID = MR.RecordID
    WHERE MR.AdmissionID = @AdmissionID;

    SET @TotalAmount = @RoomCharges + @TreatmentCharges + @MedicationCharges;

    IF EXISTS (SELECT 1 FROM Billings WHERE AdmissionID = @AdmissionID)
    BEGIN
        UPDATE Billings
        SET TotalAmount = @TotalAmount,
            BillingDate = GETDATE()
        WHERE AdmissionID = @AdmissionID;
    END
    ELSE
    BEGIN
        INSERT INTO Billings (AdmissionID, PatientID, TotalAmount, AmountPaid, BillingDate)
        VALUES (@AdmissionID, @PatientID, @TotalAmount, 0, GETDATE());
    END

    SELECT 
        B.BillingID,
        B.AdmissionID,
        B.PatientID,
        B.BillingDate,
        B.TotalAmount,
        B.AmountPaid,
        B.Balance
    FROM Billings B
    WHERE B.AdmissionID = @AdmissionID;
END;
GO
